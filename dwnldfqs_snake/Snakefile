########################################################################################################
# download_fastqs_snakemake
#   Snakemake workflow to download fastq files from SRA
#   v1.0
#   Written by David McKellar
#   Last edited: --/--/--, DWM
########################################################################################################

import pandas as pd
from itertools import chain
from functools import reduce

########################################################################################################
# Config file
########################################################################################################
configfile:'config.yaml'

########################################################################################################
# Directories and locations
########################################################################################################
TMPDIR = config['TMPDIR']
PRODIR = config['PRODIR']
METADIR = config['METADIR']

shell("mkdir -p {TMPDIR}")
shell("mkdir -p {PRODIR}")
shell("mkdir -p {METADIR}")

########################################################################################################
# Variables and references
########################################################################################################
META = pd.read_csv(config['SAMPLE_SHEET'], na_filter=False)

META = META[list(META['include'])] #remove undesired samples ('include'==False)


SRR = list(META['SRR.accession'])
# print(SRR)
for i in range(0,len(SRR)):
    SRR[i] = SRR[i].split("; ")
SRR = list(chain(*SRR))

# print(SRR[range(1,12)])

########################################################################################################
# Executables
########################################################################################################
FFQ = config["FFQ"]
PREFETCH = config["PREFETCH"]
PFD = config["PFD"]

########################################################################################################
########################################################################################################
rule all:
    input:
        expand('{METADIR}/{sample}.json', METADIR=config['METADIR'], sample=SRR), # metadata files
        expand('{METADIR}/merged_metadata.csv', METADIR=config['METADIR'])
        # expand('{PRODIR}/{sample}_1.fastq.gz', PRODIR=config['PRODIR'], sample=SRR), # R1 fastq
        # expand('{PRODIR}/{sample}_2.fastq.gz', PRODIR=config['PRODIR'], sample=SRR) # R2 fastq

#############################################
## Get metadata with `ffq`
#############################################
#TODO add a check for the SRR ID with ffq
rule get_metadata:
    output:
        METAJSON = '{METADIR}/{sample}.json'
    shell:
        """
        {FFQ} -o {output.METAJSON} {wildcards.sample}
        """

rule merge_metadata:
    input:
        expand('{METADIR}/{sample}.json', METADIR=config['METADIR'], sample=SRR)
    output:
        METACSV = '{METADIR}/merged_metadata.csv'
    run:
        df_list = list()
        for f in input:
            df_list.append(pd.read_json(f).T)

        df_out = pd.concat(df_list)

        df_out.to_csv(output.METACSV, index=False)


#############################################
## prefetch (SRA-toolkit)
#############################################
rule prefetch:
    output:
        SRA_tmp = temp("{TMPDIR}/{sample}.sra")
    params:
        TMPDIR=TMPDIR
    shell:
        """
        echo "Prefetching SRA files for {wildcards.sample}"

        {PREFETCH} \
        --verify yes \
        --max-size 999999999999 \
        --output-directory {params.TMPDIR} \
        {wildcards.sample}
        """

#############################################
## parallel-fastq-dump
##      https://github.com/rvalieris/parallel-fastq-dump
#############################################
# TODO- merge fastqs by sample?
rule get_fastqs:
    input:
        SRA_tmp = TMPDIR+"/{sample}.sra"
    output:
        R1_FQ = '{PRODIR}/data/fastqs/{sample}_1.fastq.gz',
        R2_FQ = '{PRODIR}/data/fastqs/{sample}_2.fastq.gz'
    params:
        TMPDIR=TMPDIR,
        OUTDIR = '{PRODIR}/data/fastqs/'
    threads:
        config["THREADS"]
    shell:
        # TODO: make sure multiple SRA numbers works; space-delimited?
        # TODO: add R1, R2, etc to name?
        """
        echo "Converting SRA files to .fastq's for {wildcards.sample}"

        {PFD} \
        --sra-id {input.SRA_tmp} \
        --threads {threads} \
        --outdir {params.OUTDIR} \
        --tmpdir {params.TMPDIR} \
        --split-files \
        --gzip
        """
